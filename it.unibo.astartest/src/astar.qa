System aStarTest

Dispatch moveFinished : moveFinished(X)

Context ctxAStarTest ip [host="localhost" port=8383]

QActor astar context ctxAStarTest {
	Rules {
		defaultPosition(pos(cell(0,0), n)).
		size(5,5).
		tileSize(200).
	}
	
	Plan init normal [
		demo consult('./astar.pl')
	]
	switchTo waitStart
	
	Plan waitStart [
		demo cleanStatus;
		demo loadStatus;
		demo loadInitialPosition;
		demo visitCurrent
	]
	switchTo cleanPortion // This should be a transition only on a specific "do start" message
	
	Plan cleanPortion [
		[!? findMove(L)]
			demo registerMoves(L)
		else
			println("Cannot reach the goal due to obstacles");
		
		println("---------------------")
	]
	switchTo [!? move(_, _)] doMove
	
	Plan doMove [
		demo actualizeNext;
		demo printStatus;
		
		[!? move(A, _)]
			println(A)
		else
			selfMsg moveFinished : moveFinished(true);
		[?? move(_, T)] demo registerNext(T);
		
		println("---------------------")
	] transition
		whenTime 200 -> doMove,
		whenMsg moveFinished -> stopClean
		
	Plan stopClean [
		[!? jobDone]
			endPlan "Room is now clean!" // This should be a transition to "waitStart"
		else {
			println("Cleaned a portion of the room!");
			addRule cleanMore
		}
	]
	switchTo [?? cleanMore] cleanPortion
}